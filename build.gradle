buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath "io.ebean:ebean-gradle-plugin:11.36.1"
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven'
apply plugin: 'io.ebean'

version = "${minecraft_version}-" + getVersion()
group = "com.mccivilizations"
archivesBaseName = "civilizations"

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "${minecraft_version}-${forge_version}"
    runDir = "run"
    
    mappings = "${mapping_version}"

    replace "@VERSION@", project.version
    replaceIn "Civilizations.java"

}

repositories {
    mavenCentral()
    maven {
        name 'BASE'
        url 'http://maven.teamacronymcoders.com'
    }
}

dependencies {
    compile "io.ebean:ebean:11.37.1"
    compile 'io.ebean:ebean-querybean:11.37.1'
    compile "com.h2database:h2:1.4.191"
    deobfCompile "com.teamacronymcoders.base:base:1.12.2-${base_version}-SNAPSHOT.+"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "file://" + (System.getenv("local_maven") != null ? System.getenv("local_maven") : System.getenv("bamboo_local_maven")))
        }
    }
}

ebean {
    debugLevel = 1
    queryBeans = true
}

idea {
    module {
        sourceDirs += file("$projectDir/generated")
        generatedSourceDirs += file("$projectDir/generated")
    }
}

configurations {
    apt
}

String getVersion() {
    String version = "${mod_version}"
    String branch = project.hasProperty("branch") ? project.branch : ""

    if(branch != "") {
        version += "-" + branch

        String buildNumber = getBuildNumber()
        if(buildNumber != "") {
            version += "." + buildNumber
        }
    }

    return version
}

static String getBuildNumber() {
    String buildNumber = System.getenv("BUILD_NUMBER") != null ? System.getenv("BUILD_NUMBER") : ""
    if(buildNumber == "") {
        buildNumber = System.getenv("bamboo_buildNumber") != null ? "." + System.getenv("bamboo_buildNumber") : ""
    }
    return buildNumber
}
